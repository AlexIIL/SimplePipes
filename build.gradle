plugins {
    id 'fabric-loom' version '0.8-SNAPSHOT'
}

apply plugin: 'maven-publish' // for uploading to a maven repo

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    maven { name="BuildCraft"; url="https://mod-buildcraft.com/maven" }
}

archivesBaseName = "SimplePipes"
version = "0.5.0-pre.1"

minecraft {
}

configurations {
    compile {}
}

dependencies {
    minecraft "com.mojang:minecraft:1.17-rc1"
    mappings "net.fabricmc:yarn:1.17-rc1+build.1:v2"
    modImplementation "net.fabricmc:fabric-loader:0.11.3"

    //Fabric api
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.34.8+1.17"

    // Silk API
    // modCompile "io.github.prospector.silk:SilkAPI:1.2.3-38"

    modApi "alexiil.mc.lib:libblockattributes-all:$libblockattributes_version"
    modApi "alexiil.mc.lib:libnetworkstack-base:$libnetworkstack_version"
    modApi "alexiil.mc.lib:libmultipart-base:$libmultipart_version"

    include "alexiil.mc.lib:libblockattributes-all:$libblockattributes_version"
    include "alexiil.mc.lib:libnetworkstack-base:$libnetworkstack_version"
    include "alexiil.mc.lib:libmultipart-base:$libmultipart_version"

    // Misc
    implementation "com.google.code.findbugs:jsr305:3.0.1"
    testImplementation "junit:junit:4.12"
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    it.options.release = 16
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    repositories {
        maven {
            url System.getenv("MAVEN_DIR") ?: "$projectDir/build/maven"
        }
    }
}

// #####################
//
// Extra jar section
//
// #####################

apply from: "extra_jar_def.gradle"

// Variables (that should be changed)
ext.mainName = "simplepipes"
ext.mavenGroupId = "alexiil.mc.mod";

ext.extra_jar_def__fabric_mod_json_replacements = [
  ["\$version", project.version],
  ["\$libblockattributes_version", "$libblockattributes_version"],
  ["\$libnetworkstack_version", "$libnetworkstack_version"],
  ["\$libmultipart_version", "$libmultipart_version"]
];

ext.extra_jar_def__optimised_compression = true;
ext.extra_jar_def__decompress_external_included = true;

def allIncludes = [
    "assets/simple_pipes/icon_container.png"
];

def jarDeps = [ 
    ["alexiil.mc.lib", "libblockattributes-all", "$libblockattributes_version"],
    ["alexiil.mc.lib", "libnetworkstack-base", "$libnetworkstack_version"],
    ["alexiil.mc.lib", "libmultipart-base", "$libmultipart_version"]
];

generateJar("base", ["**"], [], true);
generateJar("all", allIncludes, [], false, [], ["base"], [], jarDeps);

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}
