plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
}

apply plugin: 'maven-publish' // for uploading to a maven repo

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenLocal()
    maven { name="BuildCraft"; url="https://mod-buildcraft.com/maven" }
    maven { name="REI"; url="https://maven.shedaniel.me/" }
    maven { name="EMI"; url="https://maven.terraformersmc.com/" }
}

archivesBaseName = "SimplePipes"
version = "0.8.0"

loom {
    accessWidenerPath = file("src/main/resources/simple_pipes.accesswidener")

    // Use this to see where we depend on others' access wideners
//    enableTransitiveAccessWideners = false
}

configurations {
    compile {}
}

dependencies {
    minecraft "com.mojang:minecraft:1.19"
    mappings "net.fabricmc:yarn:1.19+build.4:v2"
    modImplementation "net.fabricmc:fabric-loader:0.14.12"

    //Fabric api
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.57.0+1.19"

    // Silk API
    // modCompile "io.github.prospector.silk:SilkAPI:1.2.3-38"

    modApi "alexiil.mc.lib:libblockattributes-all:$libblockattributes_version"
    modApi "alexiil.mc.lib:libnetworkstack-base:$libnetworkstack_version"
    modApi "alexiil.mc.lib:libmultipart-base:$libmultipart_version"
    modApi "alexiil.mc.lib:libmultipart-compat:$libmultipart_version"

    include "alexiil.mc.lib:libblockattributes-all:$libblockattributes_version"
    include "alexiil.mc.lib:libnetworkstack-base:$libnetworkstack_version"
    include "alexiil.mc.lib:libmultipart-base:$libmultipart_version"
    include "alexiil.mc.lib:libmultipart-compat:$libmultipart_version"

    // REI
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api:$rei_version"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-fabric:$rei_version"
    if (runtime_recipe_viewer == "rei") {
        modLocalRuntime "me.shedaniel:RoughlyEnoughItems-fabric:$rei_version"
    }

    // EMI
    modCompileOnly "dev.emi:emi:$emi_version:api"
    if (runtime_recipe_viewer == "emi") {
        modLocalRuntime "dev.emi:emi:$emi_version"
    }

    // Misc
    implementation "com.google.code.findbugs:jsr305:3.0.1"
    testImplementation "junit:junit:4.12"
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    it.options.release = 17
}

java.withSourcesJar()

publishing {
    repositories {
        maven {
            url System.getenv("MAVEN_DIR") ?: "$projectDir/build/maven"
        }
    }
}

// #####################
//
// Extra jar section
//
// #####################

apply from: "extra_jar_def.gradle"

// Variables (that should be changed)
ext.mainName = "simplepipes"
ext.mavenGroupId = "alexiil.mc.mod";

ext.extra_jar_def__fabric_mod_json_replacements = [
        ["\$version", project.version],
        ["\$libblockattributes_version", "$libblockattributes_version"],
        ["\$libnetworkstack_version", "$libnetworkstack_version"],
        ["\$libmultipart_version", "$libmultipart_version"],
        ["\$rei_version", "$rei_version"]
];

ext.extra_jar_def__optimised_compression = true;
ext.extra_jar_def__decompress_external_included = true;

def allIncludes = [
        "assets/simple_pipes/icon_container.png"
];

def jarDeps = [
        ["alexiil.mc.lib", "libblockattributes-all", "$libblockattributes_version"],
        ["alexiil.mc.lib", "libnetworkstack-base", "$libnetworkstack_version"],
        ["alexiil.mc.lib", "libmultipart-base", "$libmultipart_version"],
        ["alexiil.mc.lib", "libmultipart-compat", "$libmultipart_version"]
];

generateJar("base", ["**"], [], true);
generateJar("all", allIncludes, [], false, [], ["base"], [], jarDeps);

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}
